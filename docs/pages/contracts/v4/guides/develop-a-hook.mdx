import { Callout } from 'vocs/components'

# Develop a hook

In this guide, we'll develop a hook for concentrated liquidity pool. The same step will apply for liquidity book. We'll start with introducing hook template before the step by step guide section.

## Hooks template

Proceed to https://github.com/pancakeswap/pancake-v4-hooks-template for the hook template. Click `Use this template` to create a new repository based on the template.

The template requires Foundry. If you don't have Foundry installed, please follow the [installation guide](https://book.getfoundry.sh/getting-started/installation).

Once the new repository is cloned to local setup, run the following commands:
```bash
> forge install // install dependencies
> forge test // run the existing tests in the repository
```

Within both `src` and `test` there are 2 folders: `pool-cl` and `pool-bin`. If you are developing for concentrated liquidity pool, focus on pool-cl folder, otherwise pool-bin folder for the liquidity book pool type.

### BaseHook
BaseHook is a base contract where you will inherit for your hooks. It provides

1. helper method: `_hooksRegistrationBitmapFrom` to set up the callback required
2. callback method: for you to overwrite

```solidity
// Snippet from CLCounterHook.sol
import {CLBaseHook} from "./CLBaseHook.sol";

contract CLCounterHook is CLBaseHook {

  constructor(ICLPoolManager _poolManager) CLBaseHook(_poolManager) {}

  // 1. Set up callback required. in this case, 4 callback are required
  function getHooksRegistrationBitmap() external pure override returns (uint16) {
    return _hooksRegistrationBitmapFrom(
        Permissions({
            beforeInitialize: false,
            afterInitialize: false,
            beforeAddLiquidity: true,
            afterAddLiquidity: true,
            beforeRemoveLiquidity: false,
            afterRemoveLiquidity: false,
            beforeSwap: true,
            afterSwap: true,
            beforeDonate: false,
            afterDonate: false,
            beforeSwapReturnsDelta: false,
            afterSwapReturnsDelta: false,
            afterAddLiquidityReturnsDelta: false,
            afterRemoveLiquidityReturnsDelta: false
        })
    );
  }

  // 2. For each callback required, overwrite the method
  function beforeAddLiquidity(address,PoolKey calldata key, ICLPoolManager.ModifyLiquidityParams calldata, bytes calldata)
    external override poolManagerOnly returns (bytes4) {
      // implement hook logic and then return selector
      return this.beforeAddLiquidity.selector;
  }
}

```

## Step by step guide

We will develop a hook that allows [veCake](https://docs.pancakeswap.finance/products/vecake/what-is-vecake) holder to get a 50% swap fee discount when swapping through pool with this hook.

<Callout type="info">
The guide is also available in video format
<iframe width="560" height="315" src="https://www.youtube.com/embed/PDWEC3vUUMo?si=PLmWCZa2ZocAKhM_" title="YouTube video player" frameBorder="0" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture; web-share; fullscreen;"></iframe>
</Callout>

---

### Step 1: Download hook template

1. Create a new repository from pancake-v4-hooks-template: [Click here](https://github.com/new?template_name=pancake-v4-hooks-template&template_owner=pancakeswap) 
2. Clone the repository locally and run `forge install` and `forge test` to verify local setup.

---

### Step 2: Implementation idea

The flow will be as follows:
1. We need to first store the default swap fee in the hook. We can do during pool initialization, passing the swap fee in the hookData which will eventually flow to the hook. The hook can `abi.decode` the bytes hookData and store the swap fee.

![Store default swap fee in hook](/v4/guides-develop-hook-2.png)

2. The default swap fee is stored at hook now. At `beforeSwap()`, hook check if the swapper is veCake holder and return the updated swap fee accordingly through `lpFeeOverride` value.

![Store default swap fee in hook](/v4/guides-develop-hook-3.png)

---

### Step 3: Implement the hook

We'll perform the following:

1. Add `afterInitialize` and `beforeSwap` permission
2. Store default swap fee in `afterInitialize` 
2. Return the swap fee based on whether user is veCake holder in `beforeSwap`

Let's go through the implementation step by step

::::steps
#### 3.1 Add `afterInitialize` and `beforeSwap` permission
Create a file called at `src/pool-cl/VeCakeSwapDiscountHook.sol` and implement the following. The hook contract extends `CLBaseHook`.

```solidity
contract VeCakeSwapDiscountHook is CLBaseHook { // [!code focus]
    function getHooksRegistrationBitmap() external pure override returns (uint16) {
        return _hooksRegistrationBitmapFrom(
            Permissions({
                beforeInitialize: false,
                afterInitialize: true, // [!code focus]
                beforeAddLiquidity: false,
                afterAddLiquidity: false,
                beforeRemoveLiquidity: false,
                afterRemoveLiquidity: false,
                beforeSwap: true, // [!code focus]
                afterSwap: false,
                beforeDonate: false,
                afterDonate: false,
                beforeSwapReturnsDelta: false,
                afterSwapReturnsDelta: false,
                afterAddLiquidityReturnsDelta: false,
                afterRemoveLiquidityReturnsDelta: false
            })
        );
    }
}
```

#### 3.2 Store default swap fee in `afterInitialize` 
We specified `afterInitialize` permission in previous step. Thus CLPoolManager will call `hook.afterInitialize` after pool is initialized.
Now we'll implement the `afterInitialize` method to store the default lp fee.

```solidity
// mapping to store poolId 
mapping(PoolId => uint24) public poolIdToLpFee;

function afterInitialize(address, PoolKey calldata key, uint160, int24, bytes calldata hookData)
    external override returns (bytes4)
{
    // Get the intended lpFee for this pool and store in mapping
    uint24 lpFee = abi.decode(hookData, (uint24));
    poolIdToLpFee[key.toId()] = lpFee;

    return this.afterInitialize.selector;
}
```

#### 3.3 Return the lp fee based on whether user is veCake holder in `beforeSwap`
`beforeSwap` will be called before a swap happens, and the third return value `uint24 lpFeeOverride` is where we can override the swap fee.

Note the return value need to include `LPFeeLibrary.OVERRIDE_FEE_FLAG` so pool manager knows the intention is to override swap fee.

```solidity
function beforeSwap(address, PoolKey calldata key, ICLPoolManager.SwapParams calldata, bytes calldata)
    external view override poolManagerOnly returns (bytes4, BeforeSwapDelta, uint24)
{
    uint24 lpFee = poolIdToLpFee[key.toId()];

    if (veCake.balanceOf(tx.origin) >= 1 ether) {
        lpFee = lpFee / 2; 
    }

    return (this.beforeSwap.selector, BeforeSwapDeltaLibrary.ZERO_DELTA, lpFee | LPFeeLibrary.OVERRIDE_FEE_FLAG);
}
```
::::

<details>
    <summary> View complete source code here </summary>

```solidity [src/pool-cl/VeCakeSwapDiscountHook.sol]
// [!include ~/snippets/VeCakeSwapDiscountHook.sol]
```

</details>

---

### Step 4: Add Hook test

In the test, we'll test 2 scenarios:

1. when swapping as a normal user
1. when swapping as a veCake holder

Create a file called at `test/pool-cl/VeCakeSwapDiscountHook.t.sol` and copy content from below.

<details>
    <summary> View complete source code here </summary>
    <Callout type="warning">
        The assertion in the test has been simplified, in the real world, you should calculate the `amtOut` and verify it.
    </Callout>

```solidity [test/pool-cl/VeCakeSwapDiscountHook.t.sol]
// [!include ~/snippets/VeCakeSwapDiscountHook.t.sol]
```
</details>

In order to allow dynamic swap fee, the `fee` variable in poolKey must have dynamic flag set.

```solidity
key = PoolKey({
    currency0: currency0,
    currency1: currency1,
    hooks: hook,
    poolManager: poolManager,
    fee: LPFeeLibrary.DYNAMIC_FEE_FLAG, // [!code focus]
    parameters: bytes32(uint256(hook.getHooksRegistrationBitmap())).setTickSpacing(10)
});
```

---

### Step 5: Verify

Run `forge test` to verify test passing. 