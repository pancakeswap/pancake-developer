import { Callout } from 'vocs/components'

# Develop a hook

In this guide, we'll develop a hook for concentrated liquidity pool. The same step will apply for liquidity book. We'll start with introducing hook template before the step by step guide section.

## Hooks template

Proceed to https://github.com/pancakeswap/pancake-v4-hooks-template for the hook template. Click `Use this template` to create a new repository based on the template.

The template requires Foundry. If you don't have Foundry installed, please follow the [installation guide](https://book.getfoundry.sh/getting-started/installation).

Once the new repository is cloned to local setup, run the following commands:
```
> forge install // install dependencies
> forge test // run the existing tests in the repository
```

Within both `src` and `test` there are 2 folders: `pool-cl` and `pool-bin`. If you are developing for concentrated liquidity pool, focus on pool-cl folder, otherwise pool-bin folder for the liquidity book pool type.

### BaseHook
BaseHook is a base contract where you will inherit for your hooks. It provides

1. helper method: `_hooksRegistrationBitmapFrom` to set up the callback required
2. callback method: for you to overwrite

```solidity
// Snippet from CLCounterHook.sol
import {CLBaseHook} from "./CLBaseHook.sol";

contract CLCounterHook is CLBaseHook {

  constructor(ICLPoolManager _poolManager) CLBaseHook(_poolManager) {}

  // 1. Set up callback required. in this case, 4 callback are required
  function getHooksRegistrationBitmap() external pure override returns (uint16) {
    return _hooksRegistrationBitmapFrom(
        Permissions({
            beforeInitialize: false,
            afterInitialize: false,
            beforeAddLiquidity: true,
            afterAddLiquidity: true,
            beforeRemoveLiquidity: false,
            afterRemoveLiquidity: false,
            beforeSwap: true,
            afterSwap: true,
            beforeDonate: false,
            afterDonate: false,
            noOp: false
        })
    );
  }

  // 2. For each callback required, overwrite the method
  function beforeAddLiquidity(address,PoolKey calldata key, ICLPoolManager.ModifyLiquidityParams calldata, bytes calldata)
    external override poolManagerOnly returns (bytes4) {
      // implement hook logic and then return selector
      return this.beforeAddLiquidity.selector;
  }
}

```

## Step by step guide

We will develop a hook that allows [veCake](https://docs.pancakeswap.finance/products/vecake/what-is-vecake) holder to get a 50% swap fee discount when swapping through pool with this hook.

### Step 1: Download hook template

1. Create a new repository from pancake-v4-hooks-template: [Click here](https://github.com/new?template_name=pancake-v4-hooks-template&template_owner=pancakeswap) 
2. Clone the repository locally and run `forge install` and `forge test` to verify setup.

### Step 2: Implementation idea

The flow will be as follows:

1. Before swap, CLPoolManager will call `hook.beforeSwap()`.
2. At `beforeSwap()`, hook call `CLPoolManager.updateDynamicSwapFee()` to update the swap fee. This is where our hook will check and return either default swap fee or 50% discount. Pool manager will save the returned value from the value and set that as swap fee for the pool.

![High level](/v4/guides-develop-hook-1.png)

### Step 3: Implement the hook

We'll perform the following:

1. add `beforeSwap` permission
2. call `poolManager.updateDynamicSwapFee(key)` in `beforeSwap()`

Let's go through the implementation step by step!
::::steps
#### 3.1 Add `beforeSwap` permission
Create a file called at `src/pool-cl/VeCakeDiscountHook.sol` and implement the following. The hook contract extends `CLBaseHook` and `ICLDynamicFeeManager`.

```solidity
contract VeCakeDiscountHook is CLBaseHook { // [!code focus]
    function getHooksRegistrationBitmap() external pure override returns (uint16) {
        return _hooksRegistrationBitmapFrom(
            Permissions({
                beforeInitialize: false,
                afterInitialize: false,
                beforeAddLiquidity: false,
                afterAddLiquidity: false,
                beforeRemoveLiquidity: false,
                afterRemoveLiquidity: false,
                beforeSwap: true, // [!code focus]
                afterSwap: false,
                beforeDonate: false,
                afterDonate: false,
                noOp: false
            })
        );
    }
}
```

#### 3.2 Call `poolManager.updateDynamicSwapFee(key, swapFee)` in `beforeSwap()`
We specify the `beforeSwap` permission in the previous step. The CLPoolManager will call `beforeSwap` method before the swap that attached to this hook.
Now, we'll implement the `beforeSwap` method to call `poolManager.updateDynamicSwapFee(key, swapFee)`.

```solidity
function beforeSwap(address, PoolKey calldata key, ICLPoolManager.SwapParams calldata, bytes calldata)
    external
    override
    poolManagerOnly
    returns (bytes4)
{
    uint24 swapFee = getDefaultSwapFee(key);

    // Give 50% discount if the user is a veCake holder.
    if (veCake.balanceOf(tx.origin) >= 1 ether) {
        swapFee = swapFee / 2;
    }

    poolManager.updateDynamicSwapFee(key, swapFee);
    return this.beforeSwap.selector;
}

/// @return the default swap fee for the pool
function getDefaultSwapFee(PoolKey calldata key) internal view returns (uint24) {
    return key.fee & SwapFeeLibrary.STATIC_FEE_MASK;
}
```
::::

<details>
  <summary>View `VeCakeDiscountHook` complete source code</summary>

```solidity [src/pool-cl/VeCakeDiscountHook.sol]
// [!include ~/snippets/VeCakeDiscountHook.sol]
```
</details>

### Step 4: Add Hook test

In the test, we'll test 2 scenarios:

1. when swapping as a normal user
1. when swapping as a veCkae holder

<Callout type="warning">
    The assertion has been simplified, in the real world, you should calculate the `amtOut` and verify it.
</Callout>

Create a file called at `test/pool-cl/VeCakeDiscountHook.t.sol` and implement the following:

<details>
    <summary> View complete source code here </summary>

```solidity [test/pool-cl/VeCakeDiscountHook.t.sol]
// [!include ~/snippets/VeCakeDiscountHook.t.sol]
```

</details>


### Step 5: Verify

Run `forge test` to verify test passing.